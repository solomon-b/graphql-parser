

graphqlDocument
  : executableOrTypeSystemDefiniton
  | executableOrTypeSystemDefiniton graphqlDocument

executableOrTypeSystemDefiniton
  : executableDefinition
  | typeSystemDefinition

executableDocument
  : executableDefinition
  | executableDefinition executableDocument

typeSystemDocument
  : typeSystemDefinition
  | typeSystemDefinition typeSystemDocument

typeSystemDefinition
  : schemaDefinition
  | typeDefinition
  | directiveDefinition

schemaDefinition
  : description 'schema' directives '{' rootOperationTypeDefinitions '}'

rootOperationTypeDefinitions
  : rootOperationTypeDefinition
  | rootOperationTypeDefinition rootOperationTypeDefinitions

rootOperationTypeDefinition
  : operationType ':' name

typeDefinition
  : scalarTypeDefinition
  | objectTypeDefinition
  | interfaceTypeDefinition
  | unionTypeDefinition
  | enumTypeDefinition
  | inputObjectTypeDefinition

scalarTypeDefinition
  : description 'scalar' name directives

objectTypeDefinition
  : description 'type' name implementsInterfaces directives fieldsDefinition

interfaceTypeDefinition
  : description 'interface' name implementsInterfaces directives fieldsDefinition

implementsInterfaces
  : 'implements' implementsInterfaces_
  | {- empty -}

implementsInterfaces_
  : name
  | implementsInterfaces_ '&' name

fieldsDefinition
  : '{' fieldDefinitions '}'

fieldDefinitions
  : fieldDefinition
  | fieldDefinition fieldDefinitions

fieldDefinition
  : description name argumentsDefinition ':' type directives

unionTypeDefinition
  : description 'union' name directives '=' unionMembers

unionMembers
  : name
  | name '|' unionMembers

enumTypeDefinition
  : description 'enum' name directives '{' enumValuesDefinition '}'

enumValuesDefinition
  : enumValueDefinition
  | enumValueDefinition enumValuesDefinition

enumValueDefinition
  : description name directives

inputObjectTypeDefinition
  : description 'input' name directives inputFieldsDefinition

directiveDefinition
  : description 'directive' dir argumentsDefinition optRepeatable 'on' directiveLocations

directiveLocations
  : directiveLocation
  | directiveLocation '|' directiveLocations

directiveLocation
  : executableDirectiveLocation
  | typeSystemDirectiveLocation

executableDirectiveLocation
  : 'QUERY'
  | 'MUTATION'
  | 'SUBSCRIPTION'
  | 'FIELD'
  | 'FRAGMENT_DEFINITION'
  | 'FRAGMENT_SPREAD'
  | 'INLINE_FRAGMENT'
  | 'VARIABLE_DEFINITION'

typeSystemDirectiveLocation
  : 'SCHEMA'
  | 'SCALAR'
  | 'OBJECT'
  | 'FIELD_DEFINITION'
  | 'ARGUMENT_DEFINITION'
  | 'INTERFACE'
  | 'UNION'
  | 'ENUM'
  | 'ENUM_VALUE'
  | 'INPUT_OBJECT'
  | 'INPUT_FIELD_DEFINITION'

inputFieldsDefinition
  : '{' inputValuesDefinition '}'

argumentsDefinition
  : '(' inputValuesDefinition ')'
  | {- empty -}

inputValuesDefinition
  : inputValueDefinition
  | inputValueDefinition inputValuesDefinition

inputValueDefinition
  : description name ':' type optValue directives

executableDefinition
  : operationDefinition
  | fragmentDefinition

operationDefinition
  : operationType directives selectionSet
  | operationType name directives selectionSet
  | operationType variableDefinitions directives selectionSet
  | operationType name variableDefinitions directives selectionSet
  | selectionSet

fragmentDefinition
  : 'fragment' fragmentName 'on' type directives selectionSet

fragmentSpread
  : '...' fragmentName directives

inlineFragment
  : '...' 'on' name directives selectionSet
  | '...' directives selectionSet

fragmentName
  : name

field
  : aliasAndName directives
  | aliasAndName '(' arguments ')' directives
  | aliasAndName '(' arguments ')' directives selectionSet
  | aliasAndName directives selectionSet

selectionSet
  : '{' selections '}'

selections
  : selection
  | selection selections

selection
  : field
  | fragmentSpread
  | inlineFragment

optValue
  : value
  | {- empty -}

values
  : value
  | value ',' values

value
  : 'null'
  | '"' string '"'
  | float
  | int
  | bool
  | vlist
  | vobject
  | '$' ident

stringValue
  : '"' '"'
  | '"' string '"'
  | '"""' blockString '"""'

vlist
  : '[' ']'
  | '[' values ']'

vobject
  : '{' '}'
  | '{' object '}'

object
  : objectField
  | objectField ',' object

objectField
  : name ':' value

operationType
  : 'query'
  | 'mutation'
  | 'subscription'

aliasAndName
  : name ':' name
  | name

name
  : ident

arguments
  : argument
  | arguments ',' argument

argument
  : name ':' value
  | '$' name ':' value

variableDefinitions
  : '(' variableDefinitions_ ')'

variableDefinitions_
  : variableDefinition
  | variableDefinition ',' variableDefinitions

variableDefinition
  : '$' name ':' type optValue directives

type
  : name
  | type '!'
  | '[' type ']'

directives
  : directives_
  | {- empty -}

directives_
  : directive
  | directive directives_

directive
  : dir
  | dir '(' arguments ')'

description
  : stringValue
  | {- empty -}

optRepeatable
  : 'repeatable'
  | {- empty -}